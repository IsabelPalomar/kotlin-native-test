apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.squareup.sqldelight'

kotlin {
    targets {
        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") \
                              ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'ios') {
            binaries {
                framework('SharedCode')
            }
        }

        fromPreset(presets.jvm, 'android')
    }

    sourceSets {
        commonMain.dependencies {
            api 'org.jetbrains.kotlin:kotlin-stdlib-common'

            implementation "io.ktor:ktor-client:1.0.0"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.0.1"

            implementation "io.ktor:ktor-client-json:1.0.0"
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.0"
            implementation "com.squareup.sqldelight:runtime-metadata:1.1.3"
        }

        androidMain.dependencies {
            api 'org.jetbrains.kotlin:kotlin-stdlib'

            implementation "io.ktor:ktor-client-json-jvm:1.0.0"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1"
            implementation "com.squareup.sqldelight:runtime-jvm:1.1.3"


        }
    }
}

sqldelight {
    //Name of your database, might be anything you want
    WeatherDatabase {
        //The package where all entities will be generated in
        //More options https://github.com/square/sqldelight
        packageName = 'com.isabelpalomar.startmultiplatform'
    }
}


// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}